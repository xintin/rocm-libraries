/*******************************************************************************
 *
 * MIT License
 *
 * Copyright (c) 2025 Advanced Micro Devices, Inc.
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 *
 *******************************************************************************/
#ifndef MIOPEN_DONT_USE_HIP_RUNTIME_HEADERS
#include <hip/hip_fp16.h>
#include <hip/hip_runtime.h>
#endif

#include "float_types.h"

#include "activation_functions.hpp"

template <typename TI, typename TO>
__forceinline__ __device__ void activbwdperactivation(const TI* __restrict__ x,
                                                      const TI* __restrict__ y,
                                                      const TI* __restrict__ dy,
                                                      TO* __restrict__ dx,
                                                      const TI diff_scale,
                                                      const TI gamma,
                                                      const TI beta,
                                                      const TI alpha,
                                                      const float* __restrict__ bn_scale,
                                                      const float* __restrict__ bn_bias,
                                                      float* __restrict__ dscale,
                                                      float* __restrict__ dbias,
                                                      const float* __restrict__ saved_mean,
                                                      const float* __restrict__ saved_inv_variance)
{
    const int ygid      = blockIdx.y * LOCAL_SIZE_Y + threadIdx.y;
    const int adj_index = H * W * blockIdx.x + ygid;

    if(adj_index < CHANNELS * H * W)
    {
        const FLOAT_ACCUM mean      = CVT_FP32_2ACCUM(saved_mean[adj_index]);
        const FLOAT_ACCUM inv_var   = CVT_FP32_2ACCUM(saved_inv_variance[adj_index]);
        const FLOAT_ACCUM pvt_scale = CVT_FP32_2ACCUM(bn_scale[adj_index]);
        const FLOAT_ACCUM pvt_bias  = CVT_FP32_2ACCUM(bn_bias[adj_index]);
        FLOAT_ACCUM pvt_dscale{0};
        FLOAT_ACCUM pvt_dbias{0};
        FLOAT_ACCUM dxhat{0};
        FLOAT_ACCUM dxhathat{0};

        for(int n = 0; n < BATCH_SIZE; ++n)
        {
            // per (x-dims) channel load a block of data into LDS
            int index             = n * CHANNELS * H * W + adj_index;
            FLOAT_ACCUM xhat      = (CVT_FLOAT2ACCUM(x[index]) - mean) * inv_var;
            FLOAT_ACCUM act_dy[1] = {CVT_FLOAT2ACCUM(dy[index])};
            FLOAT_ACCUM act_y[1]  = {CVT_FLOAT2ACCUM(y[index])};
            FLOAT_ACCUM bn_y[1]   = {xhat * pvt_scale + pvt_bias};
            FLOAT_ACCUM bn_dy[1];
            ActivationFunction_Diff(bn_dy,
                                    act_dy,
                                    bn_y,
                                    act_y,
                                    CVT_FLOAT2ACCUM(diff_scale),
                                    CVT_FLOAT2ACCUM(gamma),
                                    CVT_FLOAT2ACCUM(beta),
                                    CVT_FLOAT2ACCUM(alpha));
            pvt_dbias += bn_dy[0];
            pvt_dscale += xhat * bn_dy[0];
            FLOAT_ACCUM tmp = pvt_scale * bn_dy[0];
            dxhat += tmp;
            dxhathat += tmp * xhat;
        }

        for(int n = 0; n < BATCH_SIZE; ++n)
        {
            int index             = n * CHANNELS * H * W + adj_index;
            FLOAT_ACCUM xhat      = (CVT_FLOAT2ACCUM(x[index]) - mean) * inv_var;
            FLOAT_ACCUM tmp       = xhat * dxhathat + dxhat;
            FLOAT_ACCUM bn_y[1]   = {xhat * pvt_scale + pvt_bias};
            FLOAT_ACCUM act_dy[1] = {CVT_FLOAT2ACCUM(dy[index])};
            FLOAT_ACCUM act_y[1]  = {CVT_FLOAT2ACCUM(y[index])};
            FLOAT_ACCUM bn_dy[1];
            ActivationFunction_Diff(bn_dy,
                                    act_dy,
                                    bn_y,
                                    act_y,
                                    CVT_FLOAT2ACCUM(diff_scale),
                                    CVT_FLOAT2ACCUM(gamma),
                                    CVT_FLOAT2ACCUM(beta),
                                    CVT_FLOAT2ACCUM(alpha));
            FLOAT_ACCUM tmp2 = BATCH_SIZE * bn_dy[0] * pvt_scale - tmp;
            FLOAT_ACCUM tmp3 = inv_var * static_cast<FLOAT_ACCUM>(1.0f / BATCH_SIZE);
            dx[index]        = CVT_ACCUM2FLOAT(tmp2 * tmp3);
        }

        // Write out data
        dbias[adj_index]  = CVT_ACCUM2FP32(pvt_dbias);
        dscale[adj_index] = CVT_ACCUM2FP32(pvt_dscale);
    }
}

extern "C" __global__ __launch_bounds__(LOCAL_SIZE_X* LOCAL_SIZE_Y) //
    void ActivBwdPerActivation(const INPUT_TYPE* __restrict__ x,
                               const INPUT_TYPE* __restrict__ y,
                               const INPUT_TYPE* __restrict__ dy,
                               OUTPUT_TYPE* __restrict__ dx,
                               const INPUT_TYPE diff_scale,
                               const INPUT_TYPE gamma,
                               const INPUT_TYPE beta,
                               const INPUT_TYPE alpha,
                               const float* __restrict__ bn_scale,
                               const float* __restrict__ bn_bias,
                               float* __restrict__ dscale,
                               float* __restrict__ dbias,
                               const float* __restrict__ saved_mean,
                               const float* __restrict__ saved_inv_variance)
{
    activbwdperactivation<INPUT_TYPE, OUTPUT_TYPE>(x,
                                                   y,
                                                   dy,
                                                   dx,
                                                   diff_scale,
                                                   gamma,
                                                   beta,
                                                   alpha,
                                                   bn_scale,
                                                   bn_bias,
                                                   dscale,
                                                   dbias,
                                                   saved_mean,
                                                   saved_inv_variance);
}
